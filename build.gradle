buildscript {
    // Build configuration
    ext.buildConfig = [
        minSdk     : 21,
        compileSdk : 29,
        targetSdk  : 29,
        versionCode: 1,
        versionName: "1.0"
    ]

    // Dependency versions
    ext.versions = [
        agp   : "3.5.2",
        kotlin: "1.3.50"
    ]

    // Dependencies
    ext.deps = [
        kotlin : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin",

        plugins: [
            agp   : "com.android.tools.build:gradle:$versions.agp",
            kotlin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        ]
    ]

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath deps.plugins.agp
        classpath deps.plugins.kotlin
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    // common kotlin configuration
    def kotlinConfig = {

        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
            kotlinOptions.jvmTarget = "1.8"
        }
    }
    plugins.withId("kotlin-android", kotlinConfig)
    plugins.withId("kotlin", kotlinConfig)
    plugins.withId("kotlin") {
        // task to run tests for kotlin-only module
        tasks.register("testDebugUnitTest", Test)
    }


    // common android configuration
    def androidConfig = {

        android {
            compileSdkVersion buildConfig.compileSdk
            defaultConfig {
                minSdkVersion buildConfig.minSdk
                targetSdkVersion buildConfig.targetSdk
                testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
            }

            compileOptions {
                sourceCompatibility = "1.8"
                targetCompatibility = "1.8"
            }

            lintOptions {
                abortOnError = true
                warningsAsErrors = true
                htmlReport = false
            }
        }
    }
    plugins.withId("com.android.application", androidConfig)
    plugins.withId("com.android.library", androidConfig)
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
